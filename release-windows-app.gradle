/*
 * Copyright 2018 Matthew Tamlin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'edu.sc.seis.launch4j'

launch4j {
  mainClassName = project.mainClassName
  jar = shadowJar.archivePath.getAbsolutePath()
  outfile = project.ext.appName + ".exe"

  windowTitle = project.ext.appName
  icon = project.ext.windowsIconPath

  version = project.ext.windowsReleaseVersion
  textVersion = project.ext.windowsReleaseVersion

  bundledJrePath = project.projectDir.absolutePath + '/deployment/JREs/windows'
  bundledJre64Bit = true
}

task buildWindowsRelease(group: 'release') {
  doFirst {
    final File jre = new File(project.projectDir, 'deployment/JREs/windows')

    if (!jre.exists()) {
      throw new RuntimeException("Windows JRE does not exist. Put JRE home in ${jre.getAbsolutePath()}.")
    }
  }
}

buildWindowsRelease.finalizedBy(createExe)
createExe.dependsOn(shadowJar)

createExe.doLast {
  println "Native Windows app release can be found at ${createExe.outputDirectory}/${createExe.outfile}"
}